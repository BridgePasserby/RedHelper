import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.zingking.redhelper"
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 3
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField 'int', 'TYPE','1'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }

    defaultConfig {
        resValue "string", "build_time", buildTime()
        resValue "string", "build_host", hostName()
        resValue "string", "build_revision", revision()
    }

    flavorDimensions "default"//这个名字貌似随便取，也可以有多个，总之一定要有..
    productFlavors {
        // preview release
        pre_release {
            buildConfigField 'int', 'TYPE', '1'
            buildConfigField 'String', 'BASE_URL', '"http://test.xxx.com"'
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "192.168.1.1"]
        }
        // publish release
        pub_release {
            buildConfigField 'String', 'BASE_URL', '"http://test.xxx.com"'
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "xiaomi"]
        }
        // developer debug
        dev_debug {
            buildConfigField 'String', 'BASE_URL', '"http://test.xxx.com"'
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "qq"]
        }
        // tester debug
        debug_tester {
            buildConfigField 'String', 'BASE_URL', '"http://test.xxx.com"'
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "qq"]
        }
    }

}


static def buildTime() {
    return new Date().format("yyyy-MM-dd HH:mm:ss")
}

static def hostName() {
    return System.getProperty("user.name") + "@" + InetAddress.localHost.hostName
}

def revision() {
    def code = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = code
    }
    return code.toString()
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation 'com.github.zcweng:switch-button:0.0.3@aar'
    implementation 'info.hoang8f:android-segmented:1.0.6'
}
repositories {
    mavenCentral()
}

android.applicationVariants.all { variant ->
//    if (variant.buildType.name == 'release') {
    def time = new SimpleDateFormat("yyyyMMddHHmm").format(System.currentTimeMillis())
    def buildName //渠道名字
    variant.outputs.each { output ->
        variant.productFlavors.each { product ->
            buildName = product.name //获取渠道名字
        }
        output.outputFileName = "${buildName}RedHelper.V${versionName}_VC${versionCode}_${time}_${variant.buildType.name}.apk"
    }
//    }
}
